def buildAndPushImage(regionName, gitUrl, ecrUrl, rawImageName, rawImageTag, buildId) {
  def imageTag  = "${rawImageTag}.${buildId}";
  def imageName = "${ecrUrl}/${rawImageName}:${imageTag}";
  git(gitUrl);
  sh """
    aws ecr get-login-password --region ${regionName} | \
    docker login --username AWS --password-stdin ${ecrUrl}
    docker build -t ${imageName} .
    docker push ${imageName}
  """;
}

def applyTerraformUpdates(accountId, regionName, projectName, environmentName) {
  def bucketName = "${accountId}-${projectName}-${environmentName}-terraform-state";
  def tableName  = "${accountId}-${projectName}-${environmentName}-terraform-lock";
  git("https://github.com/alfpedraza-aws-devops/infrastructure");
  withCredentials([
    string(credentialsId: "AWS_ACCESS_KEY_ID"    , variable: "AWS_ACCESS_KEY_ID"    ),
    string(credentialsId: "AWS_SECRET_ACCESS_KEY", variable: "AWS_SECRET_ACCESS_KEY")
  ]) {
    sh """
      cd ./${environmentName}
      terraform init \
        -input=false \
        -backend-config="bucket=${bucketName}" \
        -backend-config="key=terraform.tfstate" \
        -backend-config="region=${regionName}" \
        -backend-config="dynamodb_table=${tableName}" \
        -backend-config="encrypt=true"
      terraform apply \
        -input=false \
        -lock=true \
        -auto-approve \
        -var "account_id=${accountId}" \
        -var "region_name=${regionName}" \
        -var "project_name=${projectName}"
    """;
  }
}

def waitUntil(scriptText) {
  def isReady = false;
  for (def i = 0; i < 40; i++) {
    def textReady = sh(script: scriptText, returnStdout: true).trim();
    isReady = (!textReady.isEmpty());
    if (isReady) break;
    sleep(15);
  }
  if (!isReady) throw new Exception("Timeout!");
}

def waitUntilConfigFileIsReady(bucketName, regionName) {
  waitUntil("""
    aws s3 cp s3://${bucketName}/admin.conf ./helm/admin.conf --region ${regionName} || true
    CONFIG_FILE=\$(cat ./helm/admin.conf || echo "")
    echo CONFIG_FILE
  """);
}

def waitUntilTillerIsReady() {
  waitUntil('''
    TILLER=$(kubectl --kubeconfig=./helm/admin.conf rollout status -w deployment/tiller-deploy --namespace=kube-system || echo "")
    TILLER_NOT_FOUND=$(echo "$TILLER" | grep "(NotFound)" | head -1)
    if [$TILLER_NOT_FOUND == ""]; then echo "Found"; else echo ""; fi;
  ''');
}

def deployApp(accountId, regionName, projectName, environmentName) {
  git("https://github.com/alfpedraza-aws-devops/deployment");
  def bucketName = "${accountId}-${projectName}-private-${environmentName}-master";
  waitUntilConfigFileIsReady(bucketName, regionName);
  waitUntilTillerIsReady();
  sh """
    cd ./helm
    export KUBECONFIG=./admin.conf
    helm upgrade \
      --install \
      --wait --timeout 300 \
      ${projectName} ./aws-devops
  """;
}

node {

  stage("Build Fibonacci") {
    buildAndPushImage(
      /* regionName   */ params.REGION_NAME,
      /* gitUrl       */ "https://github.com/alfpedraza-aws-devops/fibonacci",
      /* ecrUrl       */ params.ECR_URL,
      /* rawImageName */ "aws-devops-fibonacci",
      /* rawImageTag  */ "0.1.4",
      /* buildId      */ env.BUILD_ID)
  }

  stage("Build Kubernetes API") {
    buildAndPushImage(
      /* regionName   */ params.REGION_NAME,
      /* gitUrl       */ "https://github.com/alfpedraza-aws-devops/kubernetes-api",
      /* ecrUrl       */ params.ECR_URL,
      /* rawImageName */ "aws-devops-kubernetes-api",
      /* rawImageTag  */ "0.1.11",
      /* buildId      */ env.BUILD_ID)
  }

  stage("Build Web UI") {
    buildAndPushImage(
      /* regionName   */ params.REGION_NAME,
      /* gitUrl       */ "https://github.com/alfpedraza-aws-devops/web-ui",
      /* ecrUrl       */ params.ECR_URL,
      /* rawImageName */ "aws-devops-web-ui",
      /* rawImageTag  */ "0.1.4",
      /* buildId      */ env.BUILD_ID)
  }

  stage("Update VPC") {
    applyTerraformUpdates(
      /* accountId       */ params.ACCOUNT_ID,
      /* regionName      */ params.REGION_NAME,
      /* projectName     */ params.PROJECT_NAME,
      /* environmentName */ "vpc")
  }

  stage("Update DEV environment") {
    applyTerraformUpdates(
      /* accountId       */ params.ACCOUNT_ID,
      /* regionName      */ params.REGION_NAME,
      /* projectName     */ params.PROJECT_NAME,
      /* environmentName */ "dev")
  }

  stage("Deploy to DEV environment") {
    deployApp(
      /* accountId       */ params.ACCOUNT_ID,
      /* regionName      */ params.REGION_NAME,
      /* projectName     */ params.PROJECT_NAME,
      /* environmentName */ "dev")
  }

  stage("Update PROD environment") {
    input "Deploy to PROD?"
    applyTerraformUpdates(
      /* accountId       */ params.ACCOUNT_ID,
      /* regionName      */ params.REGION_NAME,
      /* projectName     */ params.PROJECT_NAME,
      /* environmentName */ "prod")
  }

  stage("Deploy to PROD environment") {
    deployApp(
      /* accountId       */ params.ACCOUNT_ID,
      /* regionName      */ params.REGION_NAME,
      /* projectName     */ params.PROJECT_NAME,
      /* environmentName */ "prod")
  }
  
}